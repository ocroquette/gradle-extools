plugins {
    id 'groovy'
    id 'maven'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.9'
}

rootProject.group='com.github.ocroquette'
rootProject.version='1.17'

pluginBundle {
    website = 'https://github.com/ocroquette/gradle-extools'
    vcsUrl = 'https://github.com/ocroquette/gradle-extools.git'

    plugins {
        extoolsPlugin {
            id = 'com.github.ocroquette.extools'
            displayName = 'External tools plugin'
            description = 'A plugin that allows to download and use external tools like compilers or IDE automatically'
            tags = ["tools", "compilers"]
            // version = ''
        }
    }
}

dependencies {
    compile gradleApi()

    testCompile gradleTestKit()
    testCompile "org.spockframework:spock-core:1.1-groovy-2.4"
    testCompile "com.github.tomakehurst:wiremock:2.10.1"
    testCompile "org.apache.commons:commons-text:1.1"
}

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
      // Workaround for version conflict because of Spock:
      // groovy.lang.GroovyRuntimeException: Conflicting module versions. Module [groovy-all is loaded in version 2.4.12 and you are trying to load version 2.4.9
      // See https://stackoverflow.com/questions/31099214/tomcat-conflicting-module-versions-module-groovy-all-is-loaded-in-version-2-3/33262366#33262366
      force 'org.codehaus.groovy:groovy-all:2.4.12'
    }
}


task(zipDummyTools) {
    description "Creates the ZIP files for the dummy tools used in the unit tests"
    final resourceDir = new File(projectDir, "src/test/resources/extools")
    final targetDir = new File(buildDir, "test/repo")
    final toolIds = []
    resourceDir.eachFileRecurse {
        if ( it.name == "extools.conf" ) {
            toolIds += resourceDir.toPath().relativize( it.toPath() ).toFile().parentFile.toString()
        }
    }
    inputs.files(fileTree(resourceDir) {
        exclude "**/*.zip"
    })
    outputs.files( toolIds.collect { new File(targetDir, it + ".zip") })

    doLast {
        toolIds.each { toolId ->
            ant.zip(destfile: new File(targetDir, "${toolId}.zip")) {
                fileset(dir: new File(resourceDir, toolId))
            }
        }
    }
}

test.dependsOn zipDummyTools

task doRelease {
    doLast {
        def buildGradle = new File("build.gradle")
        def matcher = (buildGradle.text =~ /.*rootProject.version='([\d]+)\.([\d]+)-SNAPSHOT'/)
        def major = Integer.parseInt(matcher[0][1])
        def minor = Integer.parseInt(matcher[0][2])

        def oldVersion = "${major}.${minor}"
        def newVersion = "${major}.${minor+1}"

        ant.replace(file: buildGradle, token: "rootProject.version='${oldVersion}-SNAPSHOT'", value: "rootProject.version='${oldVersion}'")

        exec {
            commandLine "git", "commit", "-a", "-m", "v${oldVersion}"
        }
        exec {
            commandLine "./gradlew", "publishplugins"
        }
        exec {
            commandLine "git", "tag", "v${oldVersion}"
        }

        ant.replace(file: buildGradle, token: "rootProject.version='${oldVersion}'", value: "rootProject.version='${newVersion}-SNAPSHOT'")

        exec {
            commandLine "git", "commit", "-a", "-m", "Started v${newVersion}"
        }

        exec {
            commandLine "git", "push", "--tags"
        }
    }
}

